#nop include alias.tin

#nop tip for avoiding multiple firing;
#nop    one reaction for one trigger;
#nop switching to ticker for all action => no multi-triggers

/* logic:
   1. get current room name
   2. construct mapping type_lian => room_name
   3. need path for connecting room_name
   4. logic: in _tic_lian,
        if (current_room_name = map(type_lian)) then
            getAction(type_lian)
        else
            untic _tic_lian
            set target_room_name to map(type_lian)

            tick _tick_lian_wait_walk: (tick interval should be longer than all path delay interval)
                if current_room_name == target_room_name then
                    untick _tick_lian_wait_walk; tic _tic_lian
                fi

            path(current_room_name, map(type_lian))
        fi
    5. caution about delay jump zhuang and tick (solved); don't send path multiple times.. (solved)

 */
#nop initialisation------------------------------------------------------;
#nop map type_lian => room_name;
#var _list_room_lian;

#nop TODO: type & room can be customised by $myId/$myMenpai;
#list list_skill cre {shenyuan-gong;force;sword;finger;douzhuan-xingyi;parry;dodge;leg};
#list _list_type cre {shenyuan-gong;force;sword;finger;douzhuan-xingyi;parry;dodge;leg;sleep;dazuo;eat};
#list _list_room_lian cre {"密室";"花园";"花园";"花园";"观星台";"花园";"花园";"花园";"休息室";"休息室";"膳房"};

#var type_lian $list_skill[1];
#var _type_lian_bck $type_lian;

#nop func-------------------------------------------------------------------;
#func mapType2Room { #nop %1 type_lian;
    #list _list_type find {%1} _cnt;
    #local i_type $_cnt;
    #if {$i_type} {
        #var result $_list_room_lian[$i_type];
    };
    #else { #var result -};
}

#nop TODO: multi-key map?;
#func getPathBtwRoom { #nop %1 roomName1, %2 roomName2;
    #echo {%1, %2};
    #if { "%1%2" == "花园密室" } {
        #var result {bo huacao} };
    #elseif { "%1%2" == "密室%S" } {
        #var result {west} };
    #elseif { "%1%2" == "花园观星台" } {
        #var result {cai huanshi; westup; yun regenerate; #delay 3 {jump zhuang}} };
    #elseif { "%1%2" == "观星台%S" } {
        #var result {yun regenerate; #delay 3 {jump down; eastdown; out}} };
    #elseif { "%1%2" == "花园休息室" } {
        #var result {hp;w;w;n;n;e} };
    #elseif { {"%1%2"} == {"休息室%S"} } {
        #var result {w;s;s;e;e} };
    #elseif { "%1%2" == "花园膳房" } {
        #var result {w;w;n;w} };
    #elseif { {"%1%2"} == {"膳房%S"} } {
        #var result {e;s;e;e} };
    #else { #var result "" };
}

#func tic_logic {
    #var _target_room_name {@mapType2Room{$type_lian}};
    #repl _target_room_name {"} {};
    #nop #echo {need to room $_target_room_name};

    #if {"$_look_room_name" == "$_target_room_name"} {
        #nop #echo {do action of $type_lian};
        #if {!$isBusy} {@getLianAction{$type_lian}};
    };
    #else {
        #untick _tic_lian;

        #nop TODO: use EVENT to monitor current room name;
        #var _room_updated 0;
        @getPathBtwRoom{$_look_room_name;$_target_room_name};

        #nop CAUTION: tick won't be killed by class kill;
        #tic _tick_lian_wait_walk {
            #if {$_room_updated} {
                #untick _tick_lian_wait_walk;
                #tick {_tic_lian} { #local tmp @tic_logic{}} 1;
            };
        } 5;
    };
}

#func getLianAction { #nop %1 type_lian;
    #if {{%1} == {shenyuan-gong}} {
        #nop #var result {xue bo for shenyuan-gong 20};
        #var result {xiulian shenyuan-gong};
    }; #elseif {{%1} == {force}} {
        #var result {lingwu force 50};
    }; #elseif {{%1} == {sword}} {
        #var result {lian sword 50};
    }; #elseif {{%1} == {finger}} {
        #var result {lian finger 50};
    }; #elseif {{%1} == {douzhuan-xingyi}} {
        #var result {look sky};
    }; #elseif {{%1} == {parry}} {
        #var result {lingwu parry 50};
    }; #elseif {{%1} == {dodge}} {
        #var result {lian dodge 50};
    }; #elseif {{%1} == {leg}} {
        #var result {lian leg 50};
    }; #elseif {{%1} == {sleep}} {
        #var result {sleep};
    }; #elseif {{%1} == {dazuo}} {
        #var result {dazuo max};
    }; #elseif {{%1} == {eat}} {
        #var result {eat fentiao; eat baicai; eat doufu; eat qiezi; get hulu; drink hulu};
    };
}
#nop prepare-------------------------------------------------------------;
#act {^{你必须空手才能练习|学习\S+必须空手。}} {uw all}
#act {{^你要装备 (\w+) 类武器才能练习}} {uw all; wi %2}

#nop main ticker---------------------------------------------------------;
#tick {_tic_lian} { #local tmp @tic_logic{}} 1;
#tic {idle_check} {look} 300;
#delay 2 {skbrief $type_lian};

#nop triggers--------------------------------------------------------------;
#act {{^(你的体力不够练|你现在气血虚浮，练习不了|你太累了，没有办法练习|你的体力太低了|你试着练习\S+少许进步)}} {yun recover}

#action {^你需要提高基本功，不然练得再多也没有用。} {
    lingwu $type_lian 50;
}
#action {{^你领悟了\S{2}次\S+。}} {yun regenerate}
#act {{^(你现在过于疲倦,无法静下心来领悟功夫！|你现在过于疲倦，无法专心下来研读新知。|你的精神太低了。|你今天太累了，结果什么也没有学到。|你已经很累了，歇歇再读吧，身体要紧。)}} {yun regenerate}
#act {^可惜你太累了，无法集中精神来研究夜空中的星斗。} {
    #nop stuck on zhuang...;
    #if {@get_percent{$neili;$neili_max} < 10} {
        #var type_lian sleep;
    } {
        yun regenerate;
    };
};

#act {^你太累了，先歇歇吧！} {look}; #nop mark room updated, in cases of failing jump down;
#act {^你仰首望天，天上繁星点点，你顺着银河的方向看去，却发现部分的夜空被周围的树冠挡住了。} {jump zhuang}

#act {{^你略一凝神，精神看起来好多了。}} {
    #if {@get_percent{$neili;$neili_max} < 20 && @get_percent{$qi;$qi_max} > 80} {
        dazuo max;
    };
}
#act {{^(你(的)?内力不够|\( 你的内力已经枯竭，不能施用内功。\))}} {
    #if {@get_percent{$qi;$qi_max} < 5} {
        #var type_lian sleep;
    } {
        dazuo max
    }
}
#act {{^(你现在精不够，无法控制内息的流动！|你现在气血严重不足，无法满足打坐最小要求。)}} {
    #var type_lian sleep;
}

#act {{^你一觉醒来，精神抖擞地活动了几下手脚。}} {
    #var type_lian dazuo;
    #line oneshot #act {{^你将真气在体内沿脉络运行了一圈，缓缓纳入丹田，放下手，长吐了一口气。}} {
        do 3 halt;
        #var type_lian $_type_lian_bck;
    } {1};
}

#act {^{你饿得头昏眼花，直冒冷汗|你渴得眼冒金星，全身无力。}} {
    #var type_lian eat;
}
#act {^你拿起清水葫芦咕噜噜地喝了几口清水。} {
    #var type_lian $_type_lian_bck;
}

#act {{^你的\S+进步了！}} {skbrief $type_lian}
#act {{^#(\d+)/\d+$}} {
    #nop TODO: move to multiplayer;
    #var lvl_lian %2;
    #if {%2 >= $_level_lian} {

        #nop local not working with list find..;
        #line quiet #local index;
        #list list_skill find $type_lian index;

        #if {$index < @sizeof{list_skill}} {
            #line quiet #math index {$index+1};
            #var type_lian {$list_skill[$index]};
            #var _type_lian_bck $type_lian;

            skbrief $type_lian;
        } {
            #nop start dazuo script;
            t-lian;
            t+dazuo;
            dazuo max;
        };
    }
}
