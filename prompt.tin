#nop include alias,multiplayer;

#nop var-------------------------------------------------;
#var color_bg <B004040>;

#nop func------------------------------------------------;
#func getBar { #nop %1: value, #2: max value, %3 color;
    #format result %u {%3};
    #format result {$result%l} {%3};
    #math count  %1*10/%2;
    #if {$count > 0} {
        #loop 1 $count tmp {
            #var result $result|;
        };
    };
}
#func paint_bar { #nop %1: current value, %2: effective max, %3: max value, %4, %5, %6 colors;
    #var result @getBar{%1;%3;%4};
    #var result $result@getBar{@eval{%2-%1};%3;%5};
    #var result $result@getBar{@eval{%3-%2};%3;%6};
    #var result $result<B004040>;
    #nop #echo $result;
}

#var _map_NPC2JobName {
    {林震南} {护镖}
    {宋远桥} {宋远桥}
    {欧阳克} {心上人}
    {陆乘风} {破阵}
    {公孙止} {公孙止}
    {鹿杖客} {万安塔}
    {苗王} {苗疆所有主流}
    {岑其斯} {苗疆所有主流}
    {潘秀达} {苗疆所有主流}
    {韩员外} {韩员外}
    {莲花生大士} {天珠及见性成佛}
    {萧峰} {萧峰}
    {胡一刀} {胡一刀}
    {韩世忠} {韩世忠}
    {仆人} {慕容仆人}
    {孟之经} {都府刺杀}
    {慕容复} {偷学}
    {余d} {谍报}
    {欧阳敏} {暗杀}
}
#func npc2Job { #nop %1: npc name;
    #var result $_map_NPC2JobName[%1];
}


#nop act--------------------------------------------------;
/*
#2.44M,480766,3440,33,3440,3440
#1934,1934,136,1275,1275,1275
#0,101,0,0,0,1
 */
#act {{^#([0-9.]+(M|K)?),(\d+),(-?\d+),(\d+),(-?\d\d+),(\d+)$}} {
    #nop #echo {%2, %4, %5, %6, %7, %8};
    #var neili %6;
    #var neili_max %5;
    #var jingli %8;
    #var jingli_max %7;
    #var exp %2;
    #var poten %4;

    #line gag;

    #var bar_neili @paint_bar{$neili;@max{$neili_max;$neili};@eval{2*$neili_max};<ffa>;<dde>;<eee>};

    #line oneshot #act {{^#(\d+),(\d+),(\d+),(\d+),(\d\d+),(\d+)$}} {
            #var qi_max %%2;
            #var qi_efft %%3;
            #var qi %%4;
        
            #var jing_max %%5;
            #var jing_efft %%6;
            #var jing %%7;
        
            #format percent_qi @get_percent{$qi_efft;$qi_max};
            #format percent_jing @get_percent{$jing_efft;$jing_max};
        
            #var bar_qi @paint_bar{$qi;$qi_efft;$qi_max;<faa>;<fcc>;<fee>};
            #var bar_jing @paint_bar{$jing;$jing_efft;$jing_max;<aaf>;<ccf>;<eef>};
        
            #line gag;
        } {4};

}

#var target_name -;
#var _list_target_names {};
#var target_lvl -;
#var target_id -;
#var _job -;
#var job_name -;
#var job_loc -;
#var week -;
#act {{^#(\d+),(\d+),(-?\d+),(-?\d+),(0|1),(0|1)$}} {
    #var zhenqi %2;
    #var will_fight %3;
    #var food %4;
    #var drink %5;
    #var inBattle %6;
    #var isBusy %7;

    #line gag;

    #format bar1 {--<fda>=%s=<eee>--<adf>%sLv%d<eee>--<fbf>%s:%s@%s<eee>---%s--} $myName $target_name $target_lvl $_job $job_name $job_loc {$week};
    #echo {{%s} {-4}} {@truncToWidth{$bar1}};

    #format bar2 {---<faa>%s<eee>-<adf>%s<eee>-<ffa>%s<eee>-<aff>%s<eee>---Exp:%s|Poten:%s--%s:%s--[$class_opened]---}
        $qi/$qi_max(@eval{$qi*100/$qi_efft}^$percent_qi%%)
        $jing/$jing_max(@eval{$jing*100/$jing_efft}^$percent_jing%%)
        $neili/$neili_max $jingli/$jingli_max
        $exp $poten $type_lian $lvl_lian;
    #echo {{%s} {-3}} @truncToWidth{$bar2};

    #unvar bar1, bar2;
}

#var type_lian -;
#var lvl_lian -;
#var class_opened {};
#prompt {{^#(\d+),(\d+),(-?\d+),(-?\d+),(0|1),(0|1)$}} {
    ---[$bar_qi]-[$bar_jing]-[$bar_neili]-Busy:<faf>$isBusy<eee>---
} {-2}

#fun get_percent {
    #local percent 100;
    #math percent {%1*100/%2};
    #format result $percent;
}

#eve {VARIABLE UPDATED target_name} {
    @getPowerUp{};

    #list _list_target_names find {$target_name} index;
    #if {$index == 0} { #list _list_target_names ins 1 {$target_name}};
}

#nop notification-------------------------------------------------------;
/*
【江湖】宋远桥、心上人、破阵、公孙止、万安塔、苗疆所有主流等任务奖励下降，韩员外、天珠及见性成佛、萧峰、胡一刀、韩世忠等任务奖励不变，慕容仆人、都府刺杀、偷学、谍报等任务奖励上升，护镖奖励最佳。
【江湖】经推算，本周为平静之周，无特殊效果！
┌───平静之周┬──────────────────────────────────┐
│   慕容复     │无特殊效果。                                                        │
│ y~~~y 偷│剩余时间：二小时五十九分五十九秒。                                  │
│   i i      │                                                                    │
│   y濞y 学│↑谍报、都府刺杀、公孙止、胡一刀。                                  │
│ |         │→天珠及见性成佛、韩世忠、萧峰、暗杀、护镖、万安塔。                │
│       { 任│↓苗疆所有主流、心上人、宋远桥、韩员外、慕容仆人、破阵。            │

\e[0m\e[1;37m【江湖】\e[1;32m\e[2;37;0m\e[1;37m\e[31m天珠及见性成佛\e[2;37;0m\e[1;37m、\e[2;37;0m\e[1;37m\e[31m苗疆所有主流\e[2;37;0
m\e[1;37m、\e[2;37;0m\e[1;37m\e[31m偷学\e[2;37;0m\e[1;37m、\e[2;37;0m\e[1;37m\e[31m韩员外\e[2;37;0m\e[1;37m、\e[2;37;0m\e[1;37m\e[31
m韩世忠\e[2;37;0m\e[1;37m、\e[2;37;0m\e[1;37m\e[31m宋远桥\e[2;37;0m\e[1;37m等任务奖励下降，\e[2;37;0m\e[1;37m\e[2;37;0m\e[1;37m萧峰\
e[2;37;0m\e[1;37m、\e[2;37;0m\e[1;37m\e[2;37;0m\e[1;37m破阵\e[2;37;0m\e[1;37m、\e[2;37;0m\e[1;37m\e[2;37;0m\e[1;37m都府刺杀\e[2;37;0
m\e[1;37m、\e[2;37;0m\e[1;37m\e[2;37;0m\e[1;37m公孙止\e[2;37;0m\e[1;37m、\e[2;37;0m\e[1;37m\e[2;37;0m\e[1;37m谍报\e[2;37;0m\e[1;37m
、\e[2;37;0m\e[1;37m\e[2;37;0m\e[1;37m万安塔\e[2;37;0m\e[1;37m等任务奖励不变，\e[2;37;0m\e[1;37m\e[1;32m心上人\e[2;37;0m\e[1;37m、\e
[2;37;0m\e[1;37m\e[1;32m护镖\e[2;37;0m\e[1;37m、\e[2;37;0m\e[1;37m\e[1;32m暗杀\e[2;37;0m\e[1;37m、\e[2;37;0m\e[1;37m\e[1;32m胡一刀\e
[2;37;0m\e[1;37m\e[2;37;0m\e[1;37m等任务奖励上升，\e[42;1m\e[1;37m慕容仆人\e[2;37;0m\e[1;37m奖励最佳。\e[2;37;0m\e[0m
\e[0m\e[2;37;0m\e[1;37m【江湖】\e[1;37m经推算，\e[1;37m本周为\e[1;31m朱雀之周\e[1;37m，朱雀职业轻功加成提升！\e[2;37;0m\e[1;37m\e[2;
*/

#act {~^\e%*【江湖】%+本周为%+，} {
    #var week {%3};
    time;
}
#act {{^┌───(\S+)(之周|同庆)(┬|─)}} {

    #nop CAUTION: manually input meta color will be treated as plain string and only converted while output;
    #nop different from color match (SEEMS color code is not in string itself);
    #nop var week {\e[1;31m%2周};

    #if {@length{$week} > 15 || @length{$week} < 4} { #var week {%2周} };

    #line oneshot #act {{^│(.{14})│}} {
        #local _npc_name_expInc {%%2};
        #repl _npc_name_expInc { } {};
        #nop #echo {exp increased job npc: {$_npc_name_expInc}};

        #local _job_name @npc2Job{$_npc_name_expInc};
        #format _job_name {%.4s} $_job_name;

        #var week {$week<fca>↑$_job_name};
    };

    #line oneshot #act {{^│.{14}│↑(\S+)。}} {
        #local _job_names %%2;
        #repl _job_names {、} {;};
        #list _list_job_names cre $_job_names;

        #foreach *_list_job_names[%*] _index {
            #format _name {%.4s} {$_list_job_names[$_index]};
            #list _list_job_names set $_index {$_name};
        };

        #list _list_job_names simp {};
        #repl _list_job_names {;} {};
        #var week {$week<eea>$_list_job_names};
    };

    #line oneshot #act {{^│.{14}│↓(\S+)。}} {
        #local _job_names %%2;
        #repl _job_names {、} {;};
        #list _list_job_names cre $_job_names;

        #foreach *_list_job_names[%*] _index {
            #format _name {%.4s} {$_list_job_names[$_index]};
            #list _list_job_names set $_index {$_name};
        };

        #list _list_job_names simp {};
        #repl _list_job_names {;} {};
        #var week {$week<faa>$_list_job_names};
    };
}

