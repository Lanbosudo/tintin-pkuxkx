#nop include prompt,alias,multiplayer;
#nop staus check & reaction;

#nop action priority convention (0~9, 5 default):
#nop +--------------------------------------------------+
#nop |   priority  |     scenario                       |
#nop +-------------+------------------------------------+
#nop |      0      |     placeholder                    |
#nop +-------------+------------------------------------+
#nop |      1      | oneshot, (follwoing must happen)   |
#nop +-------------+------------------------------------+
#nop |      2      |     fight                          |
#nop +-------------+------------------------------------+
#nop |      3      |     job/task                       |
#nop +-------------+------------------------------------+
#nop |      4      |     walk                           |
#nop +-------------+------------------------------------+
#nop |      5      |     trivia reactions               |
#nop +-------------+------------------------------------+

#list list_descrpt cre {
    "不堪一击";"毫不足虑";"不足挂齿";"初学乍练";"勉勉强强";
    "初窥门径";"初出茅庐";"略知一二";"普普通通";"平平淡淡";
    "平淡无奇";"粗通皮毛";"半生不熟";"马马虎虎";"略有小成";
    "已有小成";"鹤立鸡群";"驾轻就熟";"青出于蓝";"融会贯通";
    "心领神会";"炉火纯青";"了然于胸";"略有大成";"已有大成";
    "豁然贯通";"出类拔萃";"无可匹敌";"技冠群雄";"神乎其技";
    "出神入化";"非同凡响";"傲视群雄";"登峰造极";"无与伦比";
    "所向披靡";"一代宗师";"精深奥妙";"神功盖世";"举世无双";
    "惊世骇俗";"撼天动地";"震古铄今";"超凡入圣";"威镇寰宇";
    "空前绝后";"天人合一";"深藏不露";"深不可测";"返璞归真";
    "不出深浅"};
#func desc2lvl { #nop %1 desc;
    #list list_descrpt find {"%1"} index;
    #math result {$index*30};
}

#eve {VARIABLE UPDATED qi} {
    #local percent 100;
    #math percent {$qi_efft*100/$qi_max};
    #if {$percent < 80 && $inBattle == 1} {
        #send {eat yao};
    } {};

    #math percent {$qi*100/$qi_efft};
    #if {$percent < 70 && $inBattle == 1} {
        #send {yun recover};

        #if {$percent <15 && $inBattle == 1} {
            halt;
            $list_exit[1];
        };
    } {};
}

#eve {VARIABLE UPDATED jing} {
    #local percent 100;
    #math percent {$qi_efft*100/$qi_max};
    #if {$percent < 80 && $inBattle == 1} {
        #send {eat dan};
    } {};

    #math percent {$jing*100/$jing_efft};
    #if {$percent < 70 && $inBattle == 1} {
        #send {yun inspire};

        #if {$percent <15 && $inBattle == 1} {
            halt;
            $list_exit[1];
        };
    } {};
}

#eve {VARIABLE UPDATED food} {
    #if {$inBattle == 0 && $isBusy == 0} {
        #if {$food < 100 } {
            #send {do 3 eat jitui};
        };
        #if {$drink < 100} {
            #send {do 3 drink};
        };
    };
}

#nop reaction for sys info;
#act {{^(重新连线完毕。|您上次是在.*连线进入，请及时核对。)}} {#class ticker read ticker.tin;#cr;sc}
#eve {VARIABLE UPDATE myId} {#if {{$myId} != {%1}} {#delay 1 @getSpecial{}} }

#act {{^这里没有jitui这种食物。}} {#line gag;}
#act {{^你所在的环境没有水源，你身上也没有容器可以解渴。|这里没有任何可以让你解渴的水源。}} {#line gag}
#act {{^你将剩下的烤鸡腿吃得干干净净。}} {drop bone}
#act {^你有很长一段时间没有备份档案了，请使用backup命令来备份。} {backup}
#act {^你获得了%+份石炭【{瑕疵|劣质}】。} {drop shi tan}
#act {^你获得了%+份玄冰【{瑕疵|劣质}】。} {drop xuan bing}
#act {^你突然感到精神一振，浑身似乎又充满了力量！} {tuna 10}

#nop reaction for picking up;
#act {{^    \S+之\S+\((Sword|Blade|Staff|Whip|Axe|Spear|Hammer|Halberd|Throwing|Dagger|Armor|Boots|Cloth|Hands|Head|Shield|Surcoat|Waist|Wrists|Finger|Neck|Heart|Shoulder|Leg|Face|Hook)\)$}} {
    #format id %l {%2}; get $id; #line quiet #unvar id; #delay pickup {look} 1;
}
#act {^    %S{☆精金|★木灵|☆玄冰|★炎晶|◎玉髓|☆月魄|★日魂|☆玄武甲|☆麒麟角|☆凤凰羽|★神龙骨}(%S {jin|mu|bing|yan|sui|yue|ri|jia|jiao|yu|gu})} {
    #format id %l {%3 %4}; get $id; #delay pickup {look} 1;
}
#act {{^    \S+【(普通|优秀|完美)】\(([^()]+)\)}} {
    #format id %l {%3}; get $id; #line quiet #unvar id; #delay pickup {look} 1;
}
#act {{^    \S+(玄冰空青|锰晶|金刚砂)【瑕疵】\(([^()]+)\)}} {
    #format id %l {%3}; get $id; #line quiet #unvar id; #delay pickup {look} 1;
}
#act {{^    \S*(黄金|白银|包袱|百草丹|牛S血竭丹|经验符|朱睛冰蟾|养精丹|金创药|水靠|(?<!地图)残片)\(([^()]+)\)$}} {
    #format id %l {%3}; get $id; #line quiet #unvar id; #delay pickup {look} 1;
}
#act {{^\S{2}(?<!【)\S+丢下\S+}} {#delay pickup {look} 1}


#nop not right to do...;
#nop #act {{^\s+\S+的尸体\(Corpse\)}} {get gold from corpse; get silver from corpse; get cash from corpse;} {4}
#nop #act {{^\s+\S+的骸骨\(Skeleton\)}} {get gold from skeleton; get silver from skeleton; get cash from skeleton;} {4}

#nop reaction of location;
#nop CAUTION: each event can only have one receiver, i.e., two event for each variable (VARIABLE UPDATE/UPDATED);
#eve {VARIABLE UPDATED _current_room_name} {
    #nop CAUTION: chinese char should be enclosed by quote;
    #if {"$_current_room_name" == "春来茶馆"} {fillall};
    #if {"$_current_room_name" == "{(黄河|长江)渡船|羊皮筏|(小)?木船}"} {yun regenerate;dazuo max};

    #var _list_db_look_id {};
}
#act {{^(你拿起船桨用力划了起来。|你跃上小舟，船就划了起来。)}} {yun regenerate;dazuo max;}
#act {{^(不知过了多久，船终于靠岸了，你累得满头大汗。|小舟终于划到近岸，你从船上走了出来。|你朝船夫挥了挥手便跨上岸去。|你沿着踏板走了上去。|绿衣少女将小船系在树枝之上，你跨上岸去。)}} {halt;yun recover}

#act {^你取出水靠，披在身上，轻轻一纵，跳入河中。} {#delay 2 {pick sword}}

#nop reaction of npc;
#nop NPC blocker---------------------------------------------;
#act {^%s杂耍人(Zashua ren)$} {east} {4};
#act {^陆大有喝道：你不是华山弟子，不得入内。} {
    #list _list_dir_input del 1;
    @getPowerUp{};hit lu dayou}
#act {^兰剑伸手拦住你，说道：“非灵鹫宫弟子请回！”} {
    #list _list_dir_input del 1;
    @getPowerUp{};hit lan jian}
#act {^梅剑伸手拦住你，说道：“非灵鹫宫弟子请回！”} {
    #list _list_dir_input del 1;
    @getPowerUp{};hit mei jian}
#act {^中年道长拦住你说道：后面是本派重地，你不是武当弟子，此去多有不便，请留步。} {
    #list _list_dir_input del 1;
    @getPowerUp{}; hit zhongnian daozhang;}
#act {^{官兵|虚通|守山弟子}%+{留下再说！|请放下兵刃。|请解兵器。}} {
    #list _list_dir_input del 1;
    uw all}

#act {^一阵风吹过，把枯乾的骸骨化成骨灰吹散了。} {look}
#act {^丑雕神情冷漠地拦住了你的去路。} {ask diao about 独孤求败}
#act {^    绿衣女郎(Girl)} {ask girl about 拜庄}
#act {^    姑苏慕容侍女 阿碧(A bi)} {ask bi about 听香剑}
#act {^    武林名宿 大轮寺掌门 金轮法王(Jinlun fawang)} {ask fawang about 风火轮}

#act {^王夫人说道：「大理有一种名种茶花，是天下的极品，一株上共开十八朵花} {#delay 2 {#send {answer 十八学士}}}
#act {^王夫人说道：「大理有一种名种茶花，八朵异色同株} {#delay 2 {#send {answer 八仙过海}}}
#act {^王夫人说道：「大理有一种名种茶花，三朵异色同株} {#delay 2 {#send {answer 风尘三侠}}}
#act {^王夫人说道：「这一种茶花，论颜色，比十八学士少了一色，偏又是驳而不纯} {#delay 2 {#send {answer 落第秀才}}}

#nop die------------------------------------------------------------------------------------------------;
#act {^你死了。} {
    #line oneshot #act {^慢慢地你终于又有了知觉... } {
            reload;
            wear all;
            @getJifa{};
        } {2};
}

#nop target, match everything of look, low priority--------------------------------------------------------------;
#nop     江湖新星「开局一双手,打怪升级虐成狗」桃花大西瓜(Guzhuthf);
#nop    广寒宫 堂主 侠之大者 ☆日行千里☆ 「太空堂」太空人(Astronaut);
#nop #act {{^\s{4}([^0-9a-zA-Z;:() ]+\s)*(\S*「\S+」)?((一|二|三|四|五|六|七|八|九|十)+位)?([^;:()]+)\(([^()]+)\)( <\S+>)?$}};
#act {{^\s{4}(\S.+)\(([^()]+)\)( (<|\[)\S+(>|\]))?$}} {
    #nop echo {reaction,match id: %3};
    #format look_id %l {%3};

    #regexp {%2} {{\S*$job_name$}} {
        follow $look_id;

        #nop jobs=====================================;
        #if {{$_job} == {db}} {
            #local codes {$job_name};
            #repl codes {|} {;};
            #list list_db_code cre {$codes};
            #foreach *list_db_code[%*] index {
                #list list_db_code set $index {#delay $index {say $list_db_code[$index]}};
                $list_db_code[$index];
            };
        };
        #nop jobs=====================================;
    };
    #regexp {%2} {{\S*$target_name$}} {
        #format target_id %l {%3};
        #delay target_look {look $target_id} 1;
    };

    #nop jobs=====================================;
    #if {{$_job} == {db}} {
        #list _list_db_look_id find {$look_id} index;
        #if {!$index} { #nop a b <= c;
            #list _list_db_look_id ins 1 {$look_id};
        } {
            #if {{$_list_db_look_id[$index]} == {$_list_db_look_id[-1]}} { #nop a b b <= b;
                #list _list_db_look_id ins -1 {$look_id};
            } { #nop a b <= a;
                #list _list_db_look_id del $index;
                #list _list_db_look_id ins -1 {$look_id};
                #list _list_db_look_id ins -1 {$look_id};
            };
        };
    };
    #nop jobs=====================================;
#nop conflict with pick-up reactions, although loading order will have natual priority (FCFS);
#nop adding in case order changes in future;
} 5.5;

#nop complement
/* deprecate now...;
张金鳌               = zhang jinao, zhang, jinao;
#line oneshot #act {^$target_name{\s+=\s(\S[^,]+)(,[^,]+)+}} {
    #nop #echo %s {id: %%2 %%3};
    #var target_id {%%2};
};
*/
#nop id here;
#act {{^((?!【)[^\w\s]+)\s+=\s([a-z ']+)(, [a-z ']+)*$}} {
    #nop #echo {match id: %3, name: %2};
    #regexp {%2} {{\S*$job_name$}} {
        #format id %l {%3};
        follow $id;

        #if {"$_job" == "ht"} {#var _ht_id $id};
    };
    #regexp {%2} {{\S*$target_name$}} {
        #format target_id %l {%3};
        #delay target_look {look $target_id} 1;
    }
}
#act {^等等，系统喘气中......} {#delay 1 {id here}}

#nop battle-------------------------------------------------------------------------------------------------------;
#act {^你战胜了%+!} {
    #echo <faa>=====================;
    #echo <faa>%0;
    #echo <faa>=====================;
} {2}

#act {{^(\S{2}(?<!【)\S+)死了}} {
    #list _list_target_names find {%2} index;
    #if {$index} {
        #list _list_target_names del $index;

        #if {!$inBattle} {
            get all from corpse;
        } {
            get silver from corpse;
            get gold from corpse;
            get cash from corpse;
            get gem from corpse;
        };


        #if {@sizeof{_list_target_names}} {
            #var target_name $_list_target_names[1];
            #delay killing_look {look;id here} 1;
        };
    };

}

#act {^看起来%1想杀死你！} {
    #var target_name {%1};

    #delay killing_look {look;id here} 1;
} {2}

/*
          他的武功看上去不堪一击，出手似乎极轻。
          你的武功看上去鹤立鸡群，出手似乎很轻。
  TT    他的境界完全看不出深浅。
    -  -    他看起来约五十多岁，身长约近八尺，体型肥胖。
   z}z   普通百姓「华丽转身」大高手(Grobot)
 yz|zy 水流觞脸上神色平和，身形凝重，稳如山岳。
 */
#act {{^\s.{10}\s([^(]+\S)(\(|正盘膝坐|脸上神色平和)([^)]+)\)?}} {
    #format _look_id %l {%4};
    #echo {look id: $_look_id};

    #regexp {%2} {{\S*$target_name$}} {

        #line oneshot #act {{^\s.*的(武功|境界)(看上去|完全看)(\S+?)(，出手|。)}} {
            #nop #echo {%s--%s} {%%4} {@desc2lvl{%%4}};
            #var target_lvl @desc2lvl{%%4};
        } {1};
    }
    {
        #line oneshot #act {{^\s.*的(武功|境界)(看上去|完全看)(\S+?)(，出手|。)}} {
            #delay 1 {#echo {%s--%s} {%%4} {@desc2lvl{%%4}}};
        } {1};
    };
}

#nop #act {{^(你未选定攻击对手|未有对手或者你和对方未处于战斗中|你刚欲动作，突然感到一阵烦闷|慢慢地你终于又有了知觉...)}} {hpbrief} {2};

#nop 当头炮决定开始跟随你一起行动。
#act {{^\S{2}(?<!【)\S+决定开始跟随你一起行动。}} {
    #echo <ffa>=====================;
    #echo <ffa>%1;
    #echo <ffa>=====================;
}

#act {{^[> ]*$}} {#line gag}

#nop menzhon----------------------------------------------------------------------------;
#act {^阿碧对你说道：去把将%1杀掉！} {#var job_name %1} 3;

#nop job songxin----------------------------------------------------------------------------;
#act {^岳不群%*给你一封盖着火漆印戳的密函。} {
    #var _job sx;

    #class songxin_get open;
    #act {{^岳不群对你道：「我这里正好有封密函，麻烦你跑一趟，交给(\S+)附近的(\S+)。」}} {
        #var job_name %%3;
        #var job_loc %%2;
        #class songxin_get kill;
    };
    #act {{^岳不群道：「多谢\S+，你赶紧把它送到(\S+)附近的(\S+)手中。」}} {
        #var job_name %%3;
        #var job_loc %%2;
        #class songxin_get kill;
    };
    #act {{^岳不群在你的耳边悄声说道：这封机密函件，火速送给(.+)附近的(.+)，好处少不了你的。}} {
        #var job_name %%3;
        #var job_loc %%2;
        #class songxin_get kill;
    };
    #class songxin_get close;

} 3;

#act {^你伸手向怀中一摸，发现密函已经不翼而飞！} {
    #line oneshot #act {{^(?!【)(\S+)说道：「嘿嘿，让本大爷来教训教训你！」}} {
        #var target_name %%2;

        #nop done in prompt, target_name update EVENT;
        #nop #line oneshot #act {^%%2死了} {#var target_name -;get han from corpse;look;};
    } {1};
} {2}

#act {^你从怀中掏出信交给%+，道：「这是岳不群先生托%+送给您的信，请收好。」} {
    #line oneshot #act {^    武林名宿 华山派掌门「君子剑」岳不群(Yue buqun)} {
        ask yue about finish;
        #var _job -;
        #var job_loc -;
        #var job_name -;
    } {1};
} {3}

#nop job flower-----------------------------------------------------------------------------;
#act {^你道：“听说花姑娘正在制作一种特殊的酱料，不止在下可否效力一二？”} {
    #line oneshot #act {{^花小贱说道：「我正在制作一种叫做\S+，主料需要(\S+?)(各)?一朵，辅料需要(\S+?)(各)?一朵，你去帮我弄来吧。」}} {
        #var job_name {%%2;%%4};
        #repl job_name {、} {;};
        #list list_flower cre $job_name;
        #nop local tmp @plist{list_flower};

        #repl job_name {;} {|};
        #var _job ht;

        bk;
    } {1};

    #class _class_ht_action open;
    #act {^    花痴 花老板(Hua laoban)} {
        ask hua about $list_flower[1];
    } {3};

    #act {^你向花老板打听有关『%+』的消息。} {
        #line oneshot #act {^花老板说道：「%+可以在%+等地被挖到。」} {
            #var job_loc %%%2;
            bk;
        } {1};
    } 3;

    #act {^你挖出来了一株{($job_name)}。} {
        #echo <aff>========================;
        #echo 挖到！;
        #echo <aff>========================<eee>;
        uw chan;
        bk;

        id; #nop get flower id;

        #class _class_ht_give open;
        #act {^    「流民」花小贱(Hua xiaojian)} {give hua $_ht_id} {3};
        #class _class_ht_give close;

    } {3};

    #act {^{($job_name)}渐渐地枯萎了。} {
        #class _class_ht_give kill;
    } {3};

    #act {^没有花铲，你怎么挖花？} {uw all;wi chan;#delay 1 {scoop flower}} 3;

    #act {^花小贱说道：「%+正是我想要的，谢谢$myName啦。」} {
        #list list_flower find {%%1} tmp_index;
        #list list_flower del $tmp_index;
    
        #list list_flower find %%1 tmp_index;
        #if {$tmp_index == 0} {
            #repl job_name {%%1|} {};
            #repl job_name {|%%1} {};
        };
        bk;
    
        #class _class_ht_give kill;
    } 3;
    
    #act {^突然窜出几个蒙面大汉，把你劫上了马车。} {xia} 3;
    
    #act {{^完成海棠任务后，你被奖励了|^你向花小贱打听有关『fail』的消息。}} {
        #var job_loc -;
        #var job_name -;
        #class _class_ht_action kill;
    } {3};

    #class _class_ht_action close;
} {3}


#nop job pu ren------------------------------------------------------------------------------;
#act {^你向仆人打听有关『job』的消息。} {
    #line oneshot #act {^仆人叹道：家贼难防，有人偷走了少爷的信件，{据传曾在『(\S+)』附近出现，你去把它找回来吧！|听说在(\S+)留下了线索，你去看看！}} {
        #var job_name {$myName.*慕容世家家贼};
        #var job_loc {%%2%%3};
    };
} 3;

#act {^你从慕容世家家贼的尸体身上搜出一封信件。} {
    #line oneshot #act {^    姑苏慕容 仆人(Pu ren)} {
        give ren xin;
        #var _job -;
        #var job_loc -;
        #var job_name -;
    };
}

#nop job hubiao------------------------------------------------------------------------------;
#act {^林震南在你的耳边悄声说道：你把这批红货送到%+那里，他已经派了个伙计名叫%+到%+附近接你，把镖车送到他那里就行了。} {
    #var job_loc %3,%1;
    #var job_name %2;
    #var _job hb;
} 3;

#act {^你累了个半死，终于把镖运到了地头。} {
    #line oneshot #act {^    林震南(Lin zhennan)} {
        ask lin about finish;
        #var _job -;
        #var job_loc -;
        #var job_name -;
    };
}
#act {^劫匪突然从暗处跳了出来，阴笑道：“红货和人命都留下来吧！。”} {
    #var target_name {$myName的 劫匪};
    look;
}
#act {^你按照%+的指示，打开密信……} {
    #line oneshot #act {^    卷走%1财物的伙计 %+(%+)} {
        #format tmp_id %l %%2;
        ask $tmp_id about 要;
        #delay 2 {ask $tmp_id about yao};

    } {4};

    #line oneshot #act {^    %1(%+)} {
        #format tmp_id %l %%1;
        give $tmp_id cai wu;
    } {4};
}
#act {^从劫匪身上掉了出来一颗%*} { put gem in dai}

#nop job juewei--------------------------------------------------------------------------------;
#act {^最近长安城内出现不少盗窃事件，有人报告%+} {
    #var job_loc {%1};
} 3;

#act {^你%+了%*罪犯，%*的金吾卫%*，可以去衙门领%*奖%*了。} {
    #echo <aff>========================<eee>;
    #echo {找到罪犯！};
    #echo <aff>========================<eee>;
    #line oneshot #act {^    京兆少尹 郭隆(Jingzhao shaoyin)} {
        ask shaoyin about 领赏;
        ask shaoyin about 任务信息;
        #class juewei kill;
        #repl class_opened {|juewei} {};
    } {4};
}
#act {^{你寻找线索消耗了太多的时间，引起了盗贼的怀疑，盗贼逃离了长安城。|你向附近的金吾卫错误地指证上铺的盗劫犯，惊动了真正的盗贼，使得他立即逃离长安城。}} {
    #echo <aff>========================<eee>;
    #echo {失败！};
    #echo <aff>========================<eee>;
    #line oneshot #act {^    京兆少尹 郭隆(Jingzhao shaoyin)} {
        ask shaoyin about 任务信息;
        #class juewei kill;
        #repl class_opened {|juewei} {};
    } {4};
}

#nop pozhen--------------------------------------------------------------------------------;
#act {{^你对陆乘风道：“听闻近来有人冒名桃花岛弟子在(\S+)布下迷阵行恶，我愿代劳除去此獠。”}} {
    #act {^陆乘风对着你点了点头。} {
        #var job_loc %2;
        #unact {^陆乘风对着你点了点头。};
        #unact {^陆乘风说道：「别跟我吹牛，你上次完成了吗？」};
    };

    #act {^陆乘风说道：「别跟我吹牛，你上次完成了吗？」} {
        #unact {^陆乘风对着你点了点头。};
        #unact {^陆乘风说道：「别跟我吹牛，你上次完成了吗？」};
    };
} 3;


#nop diebao---------------------------------------------------------------------------------;
#act {^你向余d打听有关『job』的消息。} {
    #class _class_diebao kill;
    #var _job db;

    #class _class_diebao open;

    #act {^余d在你的耳边悄声说道：为了保护接头人的安全，接头人的名字不能透露，我只能告诉你某人在%+等着你。} {
        #var job_loc {%%1};
    } 3;

    #act {^长相大致如下%+} {
        #var _job_diebao_look {%%1};
    } 3;

    #nop CAUTION: in action, variable name beginning with underscore: ${_variable_name};
    #nop CAUTION: watch out for empty variable match?;
    #var _job_diebao_look {防止误匹fdjkajfdklajfdksl};
    #act {${_job_diebao_look}} {
        ask $_look_id about 接头;

    } 3;

    #act {^你向%+打听有关『接头』的消息。} {
        #act {^%%1在你的耳边悄声说道：有人在%+给你留下了下一步行动的步骤。} {
            #var job_loc {%%%1};
        } 3;
    } 3;

    #act {^[%+]%s$myName钧鉴：} {
        read %%1;
    } 3;

    #act {^----------------------------------------------------------------------} {
        #line oneshot #act {^%+$} {
            #var _db_code {%%%1};
            #repl _db_code {，暗号是} {|};
            #repl _db_code {，暗号} {|};
            #repl _db_code {，说出} {|};

            #repl _db_code {，请找} {|};
            #repl _db_code {其人名曰} {|};
            #repl _db_code {，名唤} {|};
            #repl _db_code {会找你。} {|};
            #repl _db_code {正在%+等着你} {|&1};

            #repl _db_code {{，|　|||＿| |“|”|。|#|◎|★|O|}|〓}} {|};

            #repl _db_code {{\|\|+}} {|};
            #repl _db_code {%+|$} {&1};
            #echo {db code: $_db_code};

            #var job_name {$_db_code};
        } 1;
    } 3;

    #nop act {^%+，暗号%+，请找%+} {
        #var job_loc {%%1};
        #var _db_code {%%2};
        #var job_name {%%3};
    } 3;

    #act {^{(?!【)\S+}见状脚下抹油，“你掩护，我先撤！”，别忘了告之右丞。} {
        #class _class_diebao open;
        #act {^    大宋 右丞相兼枢密使 余d(Yu jie)} {
            t-ticker;
            ask yu about finish;
        } 3;
        #class _class_diebao close;
    } 3;

    #act {{^   \( \|     完成谍报任务之后，你被奖励了：|^你向余d打听有关『fail』的消息。}} {
        #class _class_diebao kill;
        t@ticker;
        #var job_loc -;
        #var job_name -;
    } 3;

    #class _class_diebao close;
} 3;

#nop aobai camp------------------------------------------------------------------------------;
#act {^ 道    德：  %+                     杀    气：  {疯狂|嗜血}} {
    #untick aobai;

    #regexp $class_opened {aobai} {
        #delay {aobaisc} {sc} 1800;
        #delay {aobaiNext} {aobai 300} 1860;
    };
}
#act {^你心中已没有丝毫杀气，如果真的一心向善，不如皈依我佛。} {
    #untick chanhui;
}

#nop nested argument escape test--------------------------------------------;
#nop #act {^abc$} {
    #act {^bcd$} {
        #line oneshot #act {^test %%+} {
            #echo {%%%1, %%1};
        };
    };
}
#act {$_test_test} { #echo {matched %0}};
